module Boss
uses ParseKit<<Boss>>

#==============================================================================
# BossVM
#==============================================================================
class BossVM
  PROPERTIES
    parser      = BossParser( this )
    scope       : BossScope

    stack       = BossValue[]

    internal_t  : Token
    types       = StringTable<<BossType>>()

    type_Global : BossType
    type_Int32  : BossType

    last_error  : BossError

    global_statements = CmdStatements()

  METHODS
    method init
      internal_t = Token( TokenType.EOI, "[BossVM]", 0, 0 )

      type_Global = type( "Global", BossType.OBJECT )
      type_Int32  = type( "Int32",  BossType.INT32 )

    method execute->BossValue
      local result = UndefinedBossValue
      local i = 0
      while (i < global_statements.count)
        result = global_statements[i].execute( this )
        ++i
      endWhile
      global_statements.clear
      return result

    method parse( filepath:String, source:String, scope, global_statement_buffer=null:CmdStatements )->Logical
      if (global_statement_buffer is null) global_statement_buffer = global_statements
      try
        parser.set_source( filepath, source )
        parser.parse_multi_line_statements( global_statement_buffer )
        return true
      catch (err:BossError)
        err.print
        return false
      endTry

      #{
    method resolve->Logical
      try
        BossVM = this   # assign singleton
        # TODO
        return true
      catch (err:BossError)
        last_error = err
        err.print( source )
        return false
      endTry
      }#

    method type( t:Token, name:String, attributes:Int32 )->BossType
      local result = types[ name ]
      if (result)
        if (result.t is internal_t)
          result.t = t
          result.attributes = attributes
        endIf
      else
        result = BossType( t, name, attributes )
        types[ name ] = result
      endIf
      return result

    method type( name:String, attributes=BossType.UNDEFINED:Int32 )->BossType
      return type( internal_t, name, attributes )
endClass

