module Boss
  uses ParseKit<<Boss>>

#==============================================================================
# BossVM
#==============================================================================
class BossVM
  PROPERTIES
    stack = BossValue[]
    fp    : Int32

    string_to_id_map = StringTable<<Int32>>()
    global_vars      = LookupList<<BossValue>>()

    parser      = BossParser( this )

    internal_t  : Token
    last_error  : BossError

    is_resolved : Logical
    types       = LookupList<<BossType>>()

    type_Global  : BossType
    index_Global : Int32

    global_statements = CmdStatements()
    m_on_execute      : BossMethod

    locals      = BossLocal[]

    this_type   : BossType
    this_method : BossMethod

  METHODS
    method init
      internal_t   = Token( TokenType.EOI, "[BossVM]", 0, 0 )
      type_Global  = get_type( "Global" )
      index_Global = global_vars.count
      global_vars//Global = BossValue( BossObject(type_Global) )
      clear_global_statements

    method clear_global_statements
      m_on_execute = type_Global.get_method( "on_execute" )
      m_on_execute.statements.clear

    method execute->BossValue
      try
        if (not is_resolved)
          m_on_execute.statements.add( global_statements )
          global_statements.clear
          (forEach in types).resolve
        endIf

        stack.clear
        push( global_vars[index_Global] )
        m_on_execute.call( 0 )
        clear_global_statements
        return pop

      catch ( err:BossError )
        clear_global_statements
        throw err

      endTry

    method find_local( name:String )->BossLocal?
      forEach (v in locals)
        if (v.name == name) return v
      endForEach
      return null

    method get_type( t:Token, name:String, attributes=0:Int32 )->BossType
      local result = types[ name ]
      if (result)
        if (result.t is internal_t)
          result.t = t
          result.attributes = attributes
        endIf
      else
        result = BossType( this, t, name, attributes )
        types[ name ] = result
      endIf
      return result

    method get_type( name:String, attributes=0:Int32 )->BossType
      return get_type( internal_t, name, attributes )

    method parse( filepath:String, source:String )->Logical
      is_resolved = false

      this_method = m_on_execute
      this_type = this_method.type_context

      try
        parser.set_source( filepath, source )
        loop
          if (parser.parse_multi_line_statements(global_statements)) nextIteration
          escapeLoop
        endLoop
        return true
      catch (err:BossError)
        err.print
        return false
      endTry

    method peek->BossValue
      if (not stack.count) throw BossError( "BossVM.peek() on empty stack." )
      return stack.last

    method push( value:BossValue ) [macro]
      this.stack.add( value )

    method pop->BossValue
      if (not stack.count) throw BossError( "BossVM.pop() on empty stack." )
      return stack.remove_last

    method string_to_id( st:String )->Int32
      local entry = string_to_id_map.find( st )
      if (entry) return entry.value

      local result = string_to_id_map.count
      string_to_id_map[ st ] = result
      return result
endClass

