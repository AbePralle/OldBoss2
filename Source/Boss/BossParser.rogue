module Boss
uses ParseKit<<Boss>>

#==============================================================================
# BossParser
#==============================================================================
class BossParser : Parser
  PROPERTIES
    vm               : BossVM
    parse_expression : ParseRule

  METHODS
    method init( vm )
      local rule = add( ParseRule("expression" ) )

      rule = add_nested( BinaryParseRule("compare") )
      rule.on( "==",  <<CmdCompareEQ>> )
      rule.on( "!=",  <<CmdCompareNE>> )
      rule.on( "<=",  <<CmdCompareLE>> )
      rule.on( "<",   <<CmdCompareLT>> )
      rule.on( ">=",  <<CmdCompareGE>> )
      rule.on( ">",   <<CmdCompareGT>> )

      # <<, >>, >>>
      rule = add_nested( BinaryParseRule("bit_shift") )
      rule.on( "<<",  <<CmdLeftShift>> )
      rule.on( ">>",  <<CmdRightShift>> )
      rule.on( ">>>", <<CmdRightShiftX>> )

      # ~
      rule = add_nested( BinaryParseRule("bitwise_xor") )
      rule.on( "~", <<CmdBitwiseXor>> )

      # |
      rule = add_nested( BinaryParseRule("bitwise_or") )
      rule.on( "|", <<CmdBitwiseOr>> )

      # &
      rule = add_nested( BinaryParseRule("bitwise_and") )
      rule.on( "&", <<CmdBitwiseAnd>> )

      # +, -
      rule = add_nested( BinaryParseRule("add_subtract") )
      rule.on( "+", <<CmdAdd>> )

      rule.on( "-", <<CmdSubtract>> )

      # *, /, %
      rule = add_nested( BinaryParseRule("multiply_divide_mod") )
      rule.on( "*", <<CmdMultiply>> )
      rule.on( "/", <<CmdDivide>> )
      rule.on( "%", <<CmdMod>> )

      # ^
      rule = add_nested( BinaryParseRule("power") )
      rule.on( "^", <<CmdPower>> )

      # -, !
      rule = add_nested( UnaryParseRule("negate") )
      rule.on( "-", <<CmdNegate>> )
      rule.on( "!", <<CmdBitwiseNot>> )

      # .
      rule = add_nested( ContextParseRule("member") )
      rule.on( ".",
        function( parser:BossParser, context:Cmd )->Cmd
          return parser.parse_access( context )
        endFunction
      )

      # term
      rule = add_nested( ParseRule("term") )
      rule.on( "integer",
        function( parser:BossParser )->Cmd
          local t = parser.read
          return CmdLiteralInt32(t,t->Int32)
        endFunction
      )
      rule.on( "(",
        function( parser:BossParser )->Cmd
          parser.must_consume( TokenType.SYMBOL_OPEN_PAREN )
          local result = parser.parse_expression()
          parser.must_consume( TokenType.SYMBOL_CLOSE_PAREN )
          return result
        endFunction
      )
      rule.on( "identifier",
        function (parser:BossParser )->Cmd
          local t = parser.read
          return CmdAccess( t, t->String, parser.parse_args )
        endFunction
      )
      rule.on( "string",
        function (parser:BossParser )->Cmd
          local t = parser.read
          return CmdLiteralString( t, t->String )
        endFunction
      )
      rule.on_default( (parser) => throw parser.peek.error( "Syntax error: unexpected $." (parser.peek.description) ) )

    method consume_end_commands->Logical
      local consumed_any = false
      while (consume_eols or consume(TokenType.SYMBOL_SEMICOLON)) consumed_any = true
      return consumed_any

    method next_is_end_command->Logical
      return next_is(TokenType.EOL) or next_is(TokenType.SYMBOL_SEMICOLON)

    method next_is_statement->Logical [macro]
      return (not this.peek.type.is_structural)

    method parse_access( context:Cmd )->Cmd
      local t = read
      local name = read_identifier
      local args = parse_args
      return CmdAccess( t, context, name, args )

    method parse_args( &allow_no_parens )->CmdArgs
      local has_open_paren = consume( TokenType.SYMBOL_OPEN_PAREN )
      if (not has_open_paren and not allow_no_parens) return null

      local args = CmdArgs()
      if (has_open_paren and consume(TokenType.SYMBOL_CLOSE_PAREN)) return args

      consume_eols
      local first = true
      while (first or consume(TokenType.SYMBOL_COMMA))
        consume_eols
        first = false
        args.add( parse_expression() )
        consume_eols
      endWhile

      if (has_open_paren) must_consume( TokenType.SYMBOL_CLOSE_PAREN )
      return args

    method parse_component->Logical
      consume_eols
      if (next_is(TokenType.KEYWORD_CLASS))
        parse_class
        return true
      endIf
      return false

    method parse_class
      local t = read
      local name = read_identifier
      vm.this_type = vm.get_type( t, name )
      while (parse_category) noAction
      must_consume( TokenType.KEYWORD_END_CLASS )

    method parse_category->Logical
      consume_eols
      if (consume(TokenType.KEYWORD_METHODS))
        consume_eols
        while (parse_method) noAction
        return true
      endIf
      return false

    method parse_method->Logical
      local t = peek
      if (not consume(TokenType.KEYWORD_METHOD)) return false
      local name = read_identifier
      local m = vm.this_type.get_method( t, name )
      m.reset
      if (consume(TokenType.SYMBOL_OPEN_PAREN))
        if (not next_is(TokenType.SYMBOL_CLOSE_PAREN))
          consume_eols
          local first = true
          while (first or consume(TokenType.SYMBOL_COMMA))
            first = false
            consume_eols
            local param_t = peek
            local param_name = read_identifier
            local initial_value : Cmd
            if (consume(TokenType.SYMBOL_EQUALS))
              initial_value = parse_expression()
            endIf
            m.add_param( param_t, param_name, initial_value )
            consume_eols
          endWhile
        endIf
        must_consume( TokenType.SYMBOL_CLOSE_PAREN )
      endIf
      consume( TokenType.SYMBOL_COLON )   # optional
      vm.this_method = m
      parse_multi_line_statements( m.statements )
      return true

    method parse_statements( statements:CmdStatements, end_type:TokenType )
      if (consume_eols)
        # multi-line while
        parse_multi_line_statements( statements )
        must_consume( end_type )
      else
        # single-line while
        parse_single_line_statements( statements )
        consume_eols
      endIf

    method parse_multi_line_statements( statements:CmdStatements )->Logical
      consume_end_commands
      local found_any = false
      while (next_is_statement)
        parse_statement( statements, &allow_control_structures )
        consume_end_commands
        found_any = true
      endWhile
      return found_any

    method parse_single_line_statements( statements:CmdStatements )
      while (next_is_statement)
        parse_statement( statements, &!allow_control_structures )
        if (not consume(TokenType.SYMBOL_SEMICOLON)) return
        while (consume(TokenType.SYMBOL_SEMICOLON)) noAction
      endWhile

      if (not consume(TokenType.EOL))
        if (next_is_statement)
          must_consume( TokenType.EOL )  # force an error
        endIf
      endIf

    method parse_statement( statements:CmdStatements, &allow_control_structures )
      local t = peek

      if (allow_control_structures)
        if (consume(TokenType.KEYWORD_RETURN))
          if (next_is_end_command) statements.add( CmdReturnUndefined(t) )
          else                     statements.add( CmdReturnValue(t, parse_expression()) )
          return
        endIf

        if (consume(TokenType.KEYWORD_WHILE))
          local cmd_while = CmdWhile( t, parse_expression() )
          statements.add( cmd_while )
          if (next_is_end_command)
            parse_multi_line_statements( cmd_while.statements )
            must_consume( TokenType.KEYWORD_END_WHILE )
          else
            parse_single_line_statements( cmd_while.statements )
          endIf
          return
        endIf
      endIf

      if (consume(TokenType.KEYWORD_LOCAL))
        local name = read_identifier
        local expr : Cmd
        if (consume(TokenType.SYMBOL_EQUALS)) expr = parse_expression()
        statements.add( CmdLocal(t,name,expr) )
        return
      endIf

      if (consume(TokenType.KEYWORD_PRINTLN))
        statements.add( CmdPrintln(t,parse_args(&allow_no_parens)) )
        return
      endIf

      if (consume(TokenType.KEYWORD_GLOBAL))
        local name = read_identifier
        local expr : Cmd
        if (consume(TokenType.SYMBOL_EQUALS)) expr = parse_expression()
        statements.add( CmdGlobal(t,name,expr) )
        return
      endIf

      local expr = parse_expression()
      if (consume(TokenType.SYMBOL_EQUALS))
        statements.add( CmdAssign(t,expr,parse_expression()) )
      else
        statements.add( expr )
      endIf

    method read_identifier->String
      if (not next_is(TokenType.IDENTIFIER))
        throw peek.error( "Identifier expected." )
      endIf
      return read->String
endClass


