module Boss
  uses ParseKit<<Boss>>

class BossMethod
  PROPERTIES
    type_context : BossType
    t            : Token
    name         : String
    statements   = CmdStatements()
    locals       = BossLocal[]
    param_count  : Int32
    id           : Int32

  METHODS
    method init( type_context, t, name )
      id = type_context.vm.id( name )

    method reset
      statements.clear
      locals.clear
      param_count = 0

    method add_local( t:Token, name:String, initial_value:Cmd )->Int32
      local index = locals.count + 1
      locals.add( BossLocal(t,name,initial_value,index) )
      return index

    method add_param( t:Token, name:String, initial_value:Cmd )->Int32
      ++param_count
      return add_local( t, name, initial_value )

    method call( arg_count:Int32 )
      local vm    = type_context.vm
      local stack = vm.stack
      local old_fp = vm.fp
      vm.fp = stack.count - (arg_count+1)

      while (arg_count > param_count)
        stack.remove_last
        --arg_count
      endWhile
      while (arg_count < param_count)
        stack.add( UndefinedBossValue )
        ++arg_count
      endWhile

      loop( locals.count - param_count )
        stack.add( UndefinedBossValue )
      endLoop

      stack[ vm.fp ] = execute
      stack.discard_from( vm.fp+1 )
      vm.fp = old_fp

    method execute->BossValue
      try
        local vm = type_context.vm

        local result = UndefinedBossValue
        statements.execute( vm )

        return result
      catch (result:BossReturn)
        return result.value
      endTry

    method resolve
      local vm = type_context.vm

      forEach (param_index in 0..<param_count)
        vm.locals.add( locals[param_index] )
      endForEach

      statements.resolve( vm )

      loop (param_count) vm.locals.remove_last

endClass
